#INCLUDE "MATRAR3U.CH" 
#INCLUDE "PROTHEUS.CH"

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MATR730  ³ Autor ³ Marco Bianchi         ³ Data ³ 10/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Emissao da Pr‚-Nota                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAFAT-R4                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
User Function MATRAR3U()
 
Local oReport

If TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:PrintDialog()
EndIf

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³ Marco Bianchi         ³ Data ³ 10/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()

Local oReport
Local oVendedor
Local nValor  := 0                    
Local nValNot := 0
Local nComEmi := 0
Local nComBx  := 0
Local nPercEmi := 0
Local nPercBx  := 0


#IFDEF TOP
	Local cAliasSE3  := GetNextAlias()
	Local cAliasSA3  := cAliasSE3
 #ELSE
	Local cAliasSA3  := "SA3"
	Local cAliasSE3  := "SE3"
#ENDIF

//Local oItens 
//Local nTamData  := Len(DTOC(MsDate()))



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport := TReport():New("MATRAR3",STR0001,"MTRAR3", {|oReport| ReportPrint(oReport,oVendedor,cAliasSA3,cAliasSE3)},STR0002 + " " + STR0003)  //"Emissao do Livro dos Viajantes"###"Emissao do Livro dos Viajantes de acordo com lei 14546, ART.10 - Argentina "###"intervalo informado na opcaoo Parametros."
oReport:SetLandscape() 
oReport:SetTotalInLine(.F.)

Pergunte(oReport:uParam,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao dos itens do Pedido de Vendas                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oVendedor := TRSection():New(oReport,STR0001,{"SA3","SE3","SM0"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/) //"Emissao do Livro dos Viajantes"
oVendedor:SetTotalInLine(.F.)

TRCell():New(oVendedor,"M0_NOME"    ,/*Tabela*/,STR0004 ,"@!"                           ,15                        ,/*lPixel*/,{|| SM0->M0_NOME 	                                                }) //"Razão Social da Empresa"
TRCell():New(oVendedor,"M0_ENDCOB"  ,/*Tabela*/,STR0005 ,"@!"                           ,60                        ,/*lPixel*/,{|| SM0->M0_ENDCOB                                                 }) //"Endereco"
TRCell():New(oVendedor,"M0_CGC"     ,/*Tabela*/,STR0006 ,PesqPict("SA3","A3_CGC"	    ),TamSx3("A3_CGC"	    )[1],/*lPixel*/,{|| SM0->M0_CGC                 	                                }) //"N. de CGC da empresa"
TRCell():New(oVendedor,"MV_ACTPRIN"     ,/*Tabela*/,STR0033 ,,Len(SuperGetMV("MV_ACTPRIN",,'')),/*lPixel*/,{|| SuperGetMV("MV_ACTPRIN",,'')     													}) //"Actidad Principal"
TRCell():New(oVendedor,"MV_ACTSEC"     ,/*Tabela*/,STR0034 ,,Len(SuperGetMV("MV_ACTSEC",,'')),/*lPixel*/,{|| SuperGetMV("MV_ACTSEC",,'')        													}) //"Actidad Secundaria"
TRCell():New(oVendedor,"A3_NREDUZ"  ,/*Tabela*/,STR0007	 ,PesqPict("SA3","A3_NREDUZ"    ),TamSx3("A3_NREDUZ"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_NREDUZ               	                        }) //"Vendedor"
TRCell():New(oVendedor,"A3_CGC"     ,/*Tabela*/,STR0008 ,PesqPict("SA3","A3_CGC"		),TamSx3("A3_CGC"		)[1],/*lPixel*/,{|| (cAliasSA3)->A3_CGC                                       	    }) //"N. de CGC do Vendedor"
TRCell():New(oVendedor,"A3_LEGADO"  ,/*Tabela*/,STR0009	 ,PesqPict("SA3","A3_LEGADO"	),TamSx3("A3_LEGADO"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_LEGADO                                         }) //"Legado"
TRCell():New(oVendedor,"A3_ADMISS"  ,/*Tabela*/,STR0010	 ,PesqPict("SA3","A3_ADMISS"    ),10                        ,/*lPixel*/,{|| (cAliasSA3)->A3_ADMISS                                    	    }) //"Data Admissao"
TRCell():New(oVendedor,"A3_CATEGO"  ,/*Tabela*/,STR0032	 ,PesqPict("SA3","A3_CATEGO"    ),TamSx3("A3_CATEGO"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_CATEGO                                   	    }) //"Categoria do Vendedor"
TRCell():New(oVendedor,"A3_CONTRAT" ,/*Tabela*/,STR0011 ,PesqPict("SA3","A3_CONTRAT"	),TamSx3("A3_CONTRAT"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_CONTRAT                   	                    }) //"Contrato"
TRCell():New(oVendedor,"A3_REGIAO"  ,/*Tabela*/,STR0012 ,PesqPict("SA3","A3_REGIAO"	),TamSx3("A3_REGIAO"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_REGIAO                	                        }) //"Zona"
TRCell():New(oVendedor,"A3_MERCAD"  ,/*Tabela*/,STR0013 ,PesqPict("SA3","A3_MERCAD"	),TamSx3("A3_MERCAD"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_MERCAD                  	                    }) //"Mercadoria"
TRCell():New(oVendedor,"A3_SALARIO" ,/*Tabela*/,STR0014 ,PesqPict("SA3","A3_SALARIO"	),TamSx3("A3_SALARIO"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_SALARIO		                      },,,"RIGHT") //"Salario Garantido"
TRCell():New(oVendedor,"A3_DIARIA"  ,/*Tabela*/,STR0017 ,PesqPict("SA3","A3_DIARIA"	),TamSx3("A3_DIARIA"	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_DIARIA	                              },,,"RIGHT") //"Diaria"
TRCell():New(oVendedor,"A3_OUTROS"  ,/*Tabela*/,STR0018 ,PesqPict("SA3","A3_OUTROS"	),TamSx3("A3_OUTROS" 	)[1],/*lPixel*/,{|| (cAliasSA3)->A3_OUTROS	                                        }) //"Outros"
TRCell():New(oVendedor,"MV_PAR01"  ,/*Tabela*/,STR0035 ,'@D',10,/*lPixel*/,{|| MV_PAR01  }) //"Abarca Perído   Desde"
TRCell():New(oVendedor,"MV_PAR02"  ,/*Tabela*/,STR0036 ,'@D',10,/*lPixel*/,{|| MV_PAR02	  }) //"Hasta"    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao dos Itens                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oItens := TRSection():New(oReport,STR0019,{"SE3,SC5,SC6"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/)	// "Emissao dos Itens" //"Emissao dos Itens"
oItens:SetTotalInLine(.F.)

TRCell():New(oItens,"E3_CODCLI"	  ,/*Tabela*/,STR0020 ,PesqPict("SE3","E3_CODCLI"  ),TamSx3("E3_CODCLI" 	 )[1],/*lPixel*/,{|| (cAliasSE3)->E3_CODCLI             }) //"Cliente N. "
TRCell():New(oItens,"E3_PEDIDO"  ,/*Tabela*/,STR0021 ,PesqPict("SE3","E3_PEDIDO"  ),TamSx3("E3_PEDIDO"     )[1],/*lPixel*/,{|| (cAliasSE3)->E3_PEDIDO             }) //"N. Pedido  "
TRCell():New(oItens,"C5_EMISSAO" ,/*Tabela*/,STR0022 ,PesqPict("SC5","C5_EMISSAO" ),10,/*lPixel*/,{|| SC5->C5_EMISSAO                    }) //"Data Pedido"
TRCell():New(oItens,"nValor"	  ,/*Tabela*/,STR0023 ,PesqPict("SC6","C6_VALOR"   ),TamSx3("C6_VALOR"		 )[1],/*lPixel*/,{|| nValor                   },,,"RIGHT") //"Total Pedido"
TRCell():New(oItens,"E3_NUM" 	  ,/*Tabela*/,STR0024 ,PesqPict("SE3","E3_NUM"     ),TamSx3("E3_NUM"		 )[1],/*lPixel*/,{|| (cAliasSE3)->E3_NUM                }) //"N. Nota Fiscal"
TRCell():New(oItens,"E3_EMISSAO" ,/*Tabela*/,STR0025 ,PesqPict("SE3","E3_EMISSAO" ),10,/*lPixel*/,{|| (cAliasSE3)->E3_EMISSAO            }) //"Data Nota"
TRCell():New(oItens,"nValNot"	  ,/*Tabela*/,STR0026 ,PesqPict("SD2","D2_TOTAL"   ),TamSx3("D2_TOTAL"	     )[1],/*lPixel*/,{|| nValNot                  },,,"RIGHT") //"Total Nota Fiscal"
TRCell():New(oItens,"nPercEmi"	  ,/*Tabela*/," % " ,PesqPict("SE3","E3_PORC"   ),TamSx3("E3_PORC"	     )[1],/*lPixel*/,{|| nPercEmi                  },,,"RIGHT") //"Perc Venda"
TRCell():New(oItens,"nComEmi"	  ,/*Tabela*/,STR0027 ,PesqPict("SE3","E3_COMIS"   ),TamSx3("E3_COMIS"	     )[1],/*lPixel*/,{|| nComEmi                  },,,"RIGHT") //"Comis Venda"
TRCell():New(oItens,"nPercBx"	  ,/*Tabela*/," % " ,PesqPict("SE3","E3_PORC"   ),TamSx3("E3_PORC"	     )[1],/*lPixel*/,{|| nPercBx                  },,,"RIGHT") //"Perc Baixa"
TRCell():New(oItens,"nComBx"	  ,/*Tabela*/,STR0028 ,PesqPict("SE3","E3_COMIS"   ),TamSx3("E3_COMIS"	     )[1],/*lPixel*/,{|| nComBx                   },,,"RIGHT") //"Comis Cobrança"
TRCell():New(oItens,"A3_OUTROS"	  ,/*Tabela*/,STR0018 ,PesqPict("SA3","A3_OUTROS"  ),TamSx3("A3_OUTROS"	 )[1],/*lPixel*/,{||(cAliasSA3)->A3_OUTROS         	 }) //"Outros"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Troca descricao do total dos itens                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//oReport:Section(1):SetTotalText(STR0085)	// "T O T A I S "
TRFunction():New(oItens:Cell("nValor") ,/* cID */,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.T./*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)
TRFunction():New(oItens:Cell("nValNot"),/* cID */,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.T./*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)
TRFunction():New(oItens:Cell("nComEmi"),/* cID */,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.T./*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)
TRFunction():New(oItens:Cell("nComBx") ,/* cID */,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.T./*lEndSection*/,.F./*lEndReport*/,/*lEndPage*/)

oTemp := TRSection():New(oReport,STR0029,{},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/) //"filtro pedido "
oTemp:SetTotalInLine(.F.)

TRPosition():New(oItens,"SD2",3,{|| xFilial("SD2")+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_SERIE })
TRPosition():New(oItens,"SC5",1,{|| xFilial("SE3")+(cAliasSE3)->E3_PEDIDO })

oReport:Section(2):SetEdit(.F.) 
oReport:Section(1):SetUseQuery(.F.) // Novo compomente tReport para adcionar campos de usuario no relatorio qdo utiliza query
 
oVendedor:SetLineStyle(.T.)

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³ Marco Bianchi         ³ Data ³ 10/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport,oVendedor,cAliasSA3,cAliasSE3)

Local lQuery    := .F.
#IFNDEF TOP
	Local cCondicao := ""
	Local cAliasSC6 := "SC6"
	Local cAliasSF2 := "SF2"
#ELSE	
	Local cAliasSC6 := cAliasSF2 := GetNextAlias()
#ENDIF

Local cVendedor  := ""
Local nValor     := 0
Local nValNot    := 0 
Local nComEmi := 0
Local nComBx  := 0
Local nPercEmi := 0
Local nPercBx  := 0
Local cExpr := ""
Local cExprF1 := ""
Local lImp := .T.
Local lGetDB   := AllTrim(Upper(TCGetDB())) $ "ORACLE|POSTGRES"  //.T. - Oracle, .F. - Otros manejadores
Local cOrdby	:= ""

oReport:Section(2):Cell("nValor" ):SetBlock({|| nValor  })
oReport:Section(2):Cell("nValNot"):SetBlock({|| nValNot })
oReport:Section(2):Cell("nComEmi"):SetBlock({|| nComEmi })
oReport:Section(2):Cell("nComBx" ):SetBlock({|| nComBx  })
oReport:Section(2):Cell("nPercEmi"):SetBlock({|| nPercEmi })
oReport:Section(2):Cell("nPercBx" ):SetBlock({|| nPercBx  })

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Transforma parametros Range em expressao SQL                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MakeSqlExpr(oReport:uParam)
                                 
cExpr := "%"
cExprF1 := "%"
If mv_par03 == 1
   cExpr += " SE3.E3_BAIEMI = 'E' AND "
   cExprF1 += " SE3.E3_BAIEMI = 'E' AND "
ElseIf mv_par03 == 2                  
   cExpr += " SE3.E3_BAIEMI = 'B' AND "
   cExprF1 += " SE3.E3_BAIEMI = 'B' AND "
Endif
cExpr += " SE3.E3_TIPO IN ('NF'"
cExprF1 += " SE3.E3_TIPO IN (
If mv_par06 == 1
   //cExpr += "SF2.F2_ESPECIE <> 'NCE' AND SF2.F2_ESPECIE <> 'NCP' AND "
   cExpr += ",'NCE'"
   cExprF1 += "'NCC'"
Endif
If mv_par07 == 1
   //cExpr += "SF2.F2_ESPECIE <> 'NDC' AND SF2.F2_ESPECIE <> 'NDI' AND "
   cExpr += ",'NDC'"
   cExprF1 += Iif(mv_par06 == 1,",'NDE'", "'NDE'")
Endif
cExpr += ") AND"
cExprF1 += ") AND"
cExpr += "%"
cExprF1 += "%"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Filtragem do relatório                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	If TcSrvType() <> "AS/400"
		lQuery    := .T.
		
		If mv_par06 == 1 .or. mv_par07 == 1 
			cOrdby := IIf (lGetDB ,"% ORDER BY E3_VEND,E3_EMISSAO%","% ORDER BY SE3.E3_VEND,SE3.E3_EMISSAO%")
			oReport:Section(1):BeginQuery()
			BeginSql Alias cAliasSA3
				SELECT SA3.R_E_C_N_O_ SA3REC,SE3.R_E_C_N_O_ SE3REC,
				SA3.A3_FILIAL,SA3.A3_COD,SA3.A3_NOME,SA3.A3_NREDUZ,SA3.A3_TIPO,
				SA3.A3_CGC,SA3.A3_LEGADO,SA3.A3_ADMISS,SA3.A3_CONTRAT,SA3.A3_MERCAD,
				SA3.A3_SALARIO,SA3.A3_DIARIA,SA3.A3_OUTROS,SA3.A3_REGIAO,
				SA3.A3_COMIS,SA3.A3_ALEMISS,SA3.A3_ALBAIXA,SA3.A3_CATEGO,
				SE3.E3_FILIAL,SE3.E3_VEND,SE3.E3_NUM,SE3.E3_EMISSAO,SE3.E3_SERIE,
				SE3.E3_CODCLI,SE3.E3_LOJA,SE3.E3_BASE,SE3.E3_PORC,SE3.E3_COMIS,
				SE3.E3_DATA,SE3.E3_PREFIXO,SE3.E3_PARCELA,SE3.E3_TIPO,SE3.E3_BAIEMI,
				SE3.E3_PEDIDO,SE3.E3_ORIGEM,SE3.E3_VENCTO,SF2.F2_ESPECIE,SF2.F2_MOEDA,SF2.F2_VALBRUT
				FROM %Table:SA3% SA3, %Table:SE3% SE3,%Table:SF2% SF2
				WHERE
				SE3.E3_FILIAL = %xFilial:SE3% AND
				SE3.E3_EMISSAO >= %Exp:mv_par01% AND
				SE3.E3_EMISSAO <= %Exp:mv_par02% AND
				SE3.E3_VEND >= %Exp:mv_par04% AND
				SE3.E3_VEND <= %Exp:mv_par05% AND
				SE3.%notdel% AND
				%Exp:cExpr%
				SF2.F2_DOC = SE3.E3_NUM AND
				SF2.F2_SERIE = SE3.E3_SERIE AND
				SF2.F2_SERIE = SE3.E3_SERIE AND
				(SF2.F2_MOEDA = %Exp:mv_par08% OR
				(SF2.F2_MOEDA <>%Exp:mv_par08% AND
				%Exp:mv_par09% = 1)) AND
				SA3.%notdel% AND
				SA3.A3_FILIAL = %xFilial:SA3% AND
				SA3.A3_COD   = SE3.E3_VEND					
					UNION 
					SELECT SA3.R_E_C_N_O_ SA3REC,SE3.R_E_C_N_O_ SE3REC,
					SA3.A3_FILIAL,SA3.A3_COD,SA3.A3_NOME,SA3.A3_NREDUZ,SA3.A3_TIPO,
					SA3.A3_CGC,SA3.A3_LEGADO,SA3.A3_ADMISS,SA3.A3_CONTRAT,SA3.A3_MERCAD,
					SA3.A3_SALARIO,SA3.A3_DIARIA,SA3.A3_OUTROS,SA3.A3_REGIAO,
					SA3.A3_COMIS,SA3.A3_ALEMISS,SA3.A3_ALBAIXA,SA3.A3_CATEGO,
					SE3.E3_FILIAL,SE3.E3_VEND,SE3.E3_NUM,SE3.E3_EMISSAO,SE3.E3_SERIE,
					SE3.E3_CODCLI,SE3.E3_LOJA,SE3.E3_BASE,SE3.E3_PORC,SE3.E3_COMIS,
					SE3.E3_DATA,SE3.E3_PREFIXO,SE3.E3_PARCELA,SE3.E3_TIPO,SE3.E3_BAIEMI,
					SE3.E3_PEDIDO,SE3.E3_ORIGEM,SE3.E3_VENCTO,SF1.F1_ESPECIE,SF1.F1_MOEDA,SF1.F1_VALBRUT
					FROM %Table:SA3% SA3, %Table:SE3% SE3,%Table:SF1% SF1
					WHERE
					SE3.E3_FILIAL = %xFilial:SE3% AND
					SE3.E3_EMISSAO >= %Exp:mv_par01% AND
					SE3.E3_EMISSAO <= %Exp:mv_par02% AND
					SE3.E3_VEND >= %Exp:mv_par04% AND
					SE3.E3_VEND <= %Exp:mv_par05% AND
					SE3.%notdel% AND
					%Exp:cExprF1%
					SF1.F1_DOC = SE3.E3_NUM AND
					SF1.F1_SERIE = SE3.E3_SERIE AND
					SF1.F1_SERIE = SE3.E3_SERIE AND
					(SF1.F1_MOEDA = %Exp:mv_par08% OR
					(SF1.F1_MOEDA <>%Exp:mv_par08% AND
					%Exp:mv_par09% = 1)) AND
					SA3.%notdel% AND
					SA3.A3_FILIAL = %xFilial:SA3% AND
					SA3.A3_COD   = SE3.E3_VEND			 
				%Exp:cOrdby%
			EndSql			
			oReport:section(1):endQuery()
		Else
			oReport:Section(1):BeginQuery()
			BeginSql Alias cAliasSA3
				SELECT SA3.R_E_C_N_O_ SA3REC,SE3.R_E_C_N_O_ SE3REC,
				SA3.A3_FILIAL,SA3.A3_COD,SA3.A3_NOME,SA3.A3_NREDUZ,SA3.A3_TIPO,
				SA3.A3_CGC,SA3.A3_LEGADO,SA3.A3_ADMISS,SA3.A3_CONTRAT,SA3.A3_MERCAD,
				SA3.A3_SALARIO,SA3.A3_DIARIA,SA3.A3_OUTROS,SA3.A3_REGIAO,
				SA3.A3_COMIS,SA3.A3_ALEMISS,SA3.A3_ALBAIXA,SA3.A3_CATEGO,
				SE3.E3_FILIAL,SE3.E3_VEND,SE3.E3_NUM,SE3.E3_EMISSAO,SE3.E3_SERIE,
				SE3.E3_CODCLI,SE3.E3_LOJA,SE3.E3_BASE,SE3.E3_PORC,SE3.E3_COMIS,
				SE3.E3_DATA,SE3.E3_PREFIXO,SE3.E3_PARCELA,SE3.E3_TIPO,SE3.E3_BAIEMI,
				SE3.E3_PEDIDO,SE3.E3_ORIGEM,SE3.E3_VENCTO,SF2.F2_ESPECIE,SF2.F2_MOEDA,SF2.F2_VALBRUT
				FROM %Table:SA3% SA3, %Table:SE3% SE3,%Table:SF2% SF2
				WHERE
				SE3.E3_FILIAL = %xFilial:SE3% AND
				SE3.E3_EMISSAO >= %Exp:mv_par01% AND
				SE3.E3_EMISSAO <= %Exp:mv_par02% AND
				SE3.E3_VEND >= %Exp:mv_par04% AND
				SE3.E3_VEND <= %Exp:mv_par05% AND
				SE3.%notdel% AND
				%Exp:cExpr%
				SF2.F2_DOC = SE3.E3_NUM AND
				SF2.F2_SERIE = SE3.E3_SERIE AND
				SF2.F2_SERIE = SE3.E3_SERIE AND
				(SF2.F2_MOEDA = %Exp:mv_par08% OR
				(SF2.F2_MOEDA <>%Exp:mv_par08% AND
				%Exp:mv_par09% = 1)) AND
				SA3.%notdel% AND
				SA3.A3_FILIAL = %xFilial:SA3% AND
				SA3.A3_COD   = SE3.E3_VEND
				ORDER BY SE3.E3_VEND,SE3.E3_EMISSAO
			EndSql
			oReport:section(1):endQuery()
		EndIf
	Else
#ENDIF
      cAliasSA3  := "SA3"
	  dbSelectArea(cAliasSA3)
	  cKey := IndexKey()	   
	  cCondicao := '( A3_FILIAL == "'+xFilial("SA3")+'" .And. A3_COD >="'+mv_par04+'" .And. A3_COD <="'+mv_par05+'")'
	  oReport:Section(1):SetFilter(cCondicao,"",,cAliasSA3)	
	  oReport:SetMeter(RecCount())		// Total de Elementos da regua
	  DbGoTop()
#IFDEF TOP
   Endif
#ENDIF	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio da impressao do fluxo do relatório                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oReport:SetMeter(RecCount())
While !oReport:Cancel() .And. !((cAliasSA3)->(Eof())) .and. xFilial("SA3")==(cAliasSA3)->A3_FILIAL
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa a validacao dos filtros do usuario           	     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAliasSA3)
	
	If !lQuery
		dbSelectArea(cAliasSE3)
		dbSetOrder(2)
		dbSeek(xFilial("SE3")+(cAliasSA3)->A3_COD)
		
		If (cAliasSE3)->E3_EMISSAO < mv_par01 .or.(cAliasSE3)->E3_EMISSAO > mv_par02
			dbSkip()
			Loop
			If mv_par03 == 1 .And. SE3->E3_BAIEMI <> 'E'
				dbSkip()
				Loop
			ElseIf mv_par03 == 2 .And. SE3->E3_BAIEMI <> 'B'
				dbSkip()
				Loop
			Endif
		EndIf
	EndIf
	
	cVendedor  := (cAliasSA3)->A3_COD
	
	lImp := .T.
	While (!oReport:Cancel() .And. !((cAliasSE3)->(Eof())) .And. xFilial("SE3")==(cAliasSE3)->E3_FILIAL .and.;
		(cAliasSE3)->E3_VEND == cVendedor .and.(cAliasSE3)->E3_EMISSAO >= mv_par01 .and. (cAliasSE3)->E3_EMISSAO <= mv_par02 )
		
		IF lImp
			oReport:Section(1):Init()
			oReport:Section(1):PrintLine()
			oReport:Section(1):Finish()
			
			oReport:Section(2):Init()
			lImp := .F.
		EndIf
		
		dbSelectArea("SC5")
		dbSeek(xFilial("SC5")+(cAliasSE3)->E3_PEDIDO)
		
		If mv_par09 == 2 .And. mv_par08 <> SC5->C5_MOEDA
			(cAliasSE3)->(DbSkip())
			Loop
		Endif
		
		If !lQuery
			dbSelectArea(cAliasSF2)
			dbSeek(xFilial(cAliasSF2)+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_SERIE)
			If mv_par06 == 2 .And. (Alltrim((cAliasSF2)->F2_ESPECIE) == "NCE" .Or. Alltrim((cAliasSF2)->F2_ESPECIE) == "NCP")
				(cAliasSE3)->(DbSkip())
				Loop
			Endif
			
			If mv_par07 == 2 .And. (Alltrim((cAliasSF2)->F2_ESPECIE) == "NDC" .Or. Alltrim((cAliasSF2)->F2_ESPECIE) == "NDI" )
				(cAliasSE3)->(DbSkip())
				Loop
			Endif
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma Valor do Pedido de Vendas                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		#IFDEF TOP
			oReport:Section(3):BeginQuery()
			BeginSql Alias cAliasSC6
				SELECT sum(C6_VALOR) VALOR
				FROM %Table:SC6% SC6
				WHERE C6_FILIAL = %xFilial:SC6%
				AND C6_NUM = %Exp:(cAliasSE3)->E3_PEDIDO%
				AND SC6.%notdel%
				GROUP BY C6_NUM
			EndSql
			oReport:Section(3):EndQuery()
			nValor := (cAliasSC6)->VALOR
			
			(cAliasSC6)->(DbCloseArea())
		#ELSE
			dbSelectArea(cAliasSC6)
			dbSeek(xFilial(cAliasSC6)+(cAliasSE3)->E3_PEDIDO)
			While !Eof() .And. (cAliasSC6)->C6_FILIAL == xFilial(cAliasSC6) .And. (cAliasSC6)->C6_NUM == (cAliasSE3)->E3_PEDIDO
				nValor += (cAliasSC6)->C6_VALOR
				(cAliasSC6)->(DbSkip())
			Enddo
		#ENDIF
		
		nValor := xMoeda(nValor,SC5->C5_MOEDA,MV_PAR08,DDataBase)
	    
	    If !lQuery
			nValNot := xMoeda((cAliasSF2)->F2_VALBRUT,(cAliasSF2)->F2_MOEDA,MV_PAR08,DDataBase)
		   	nComEmi := IIF ((cAliasSE3)->E3_BAIEMI == "E",xMoeda((cAliasSE3)->E3_COMIS,SC5->C5_MOEDA,MV_PAR08,DDataBase),0)
	    	nComBx  := IIF ((cAliasSE3)->E3_BAIEMI == "B",xMoeda((cAliasSE3)->E3_COMIS,SC5->C5_MOEDA,MV_PAR08,DDataBase),0)	
		   	nPercEmi := IIF ((cAliasSE3)->E3_BAIEMI == "E",(cAliasSA3)->E3_PORC,0)
	    	nPercBx  := IIF ((cAliasSE3)->E3_BAIEMI == "B",(cAliasSA3)->E3_PORC,0)				
		Else
			If AllTrim((cAliasSA3)->F2_ESPECIE) $ ("NCC|NDE")
				nValNot := (xMoeda((cAliasSA3)->F2_VALBRUT,(cAliasSA3)->F2_MOEDA,MV_PAR08,DDataBase))* -1
			Else
				nValNot := xMoeda((cAliasSA3)->F2_VALBRUT,(cAliasSA3)->F2_MOEDA,MV_PAR08,DDataBase)
			EndIf
		   	nComEmi := IIF ((cAliasSE3)->E3_BAIEMI == "E",xMoeda((cAliasSA3)->E3_COMIS,SC5->C5_MOEDA,MV_PAR08,DDataBase),0)
	    	nComBx  := IIF ((cAliasSE3)->E3_BAIEMI == "B",xMoeda((cAliasSA3)->E3_COMIS,SC5->C5_MOEDA,MV_PAR08,DDataBase),0)	
		   	nPercEmi := IIF ((cAliasSE3)->E3_BAIEMI == "E",(cAliasSA3)->E3_PORC,0)
	    	nPercBx  := IIF ((cAliasSE3)->E3_BAIEMI == "B",(cAliasSA3)->E3_PORC,0)			
	    EndIf	
	    
		oReport:Section(2):PrintLine()
		
		dbSelectArea(cAliasSE3)
		dbSkip()
		
		//zera totais 
		nValor  := 0
		nValNot :=0 
		
		oReport:IncMeter()
		
	EndDo
	
	If !lImp
		oReport:Section(2):SetTotalText ("Totais")
		oReport:Section(2):Finish()
	EndIf
	
	If ! lQuery
		dbSelectArea(cAliasSA3)
		dbSkip()
	Endif
	
	If cVendedor <> (cAliasSA3)->A3_COD
		oReport:PrintText(STR0030,2250,500 ) //"Firma do Viajante"
		oReport:PrintText(STR0031,2250,1900) //"Firma e Selo da Empresa"
		oReport:Section(1):SetPageBreak(.T.)
	EndIf
	(cAliasSA3)->(dbSkip())
EndDo

If lQuery
	DbSelectArea(cAliasSA3)
	DbcloseArea()
Else
	DbSelectArea(cAliasSA3)
	RetIndex(cAliasSA3)
EndIf


Return
