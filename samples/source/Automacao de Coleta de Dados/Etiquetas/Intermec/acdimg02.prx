/*
Padrao Intermec
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³IMG02     ºAutor  ³Sandro Valex        º Data ³  19/06/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Ponto de entrada referente a imagem de identificacao da     º±±
±±º          ³endereco                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function Img02 // imagem de etiqueta de ENDERECO
Local cCodigo
Local cCodID := paramixb[1]
If cCodID # NIL
	cCodigo := cCodID
ElseIf Empty(SBE->BE_IDETIQ)
	If Usacb0('02')
		cCodigo := CBGrvEti('02',{SBE->BE_LOCALIZ,SBE->BE_LOCAL})
		RecLock("SBE",.F.)
		SBE->BE_IDETIQ := cCodigo
		MsUnlock()
	Else
		cCodigo :=SBE->(BE_LOCAL+BE_LOCALIZ)
	EndIf
Else
	If Usacb0('02')
		cCodigo := SBE->BE_IDETIQ
	Else
		cCodigo :=SBE->(BE_LOCAL+BE_LOCALIZ)
	EndIf
Endif
cCodigo := Alltrim(cCodigo)
// MSCBLOADGRF("SIGA.BMP")
MSCBBEGIN(1,3)
	MSCBBOX(01,02,34,76,1)
	MSCBLineV(30,30,76,1)
	MSCBLineV(23,02,76,1)
	MSCBLineV(15,02,76,1)
	MSCBLineH(23,30,34,1)
//	MSCBGRAFIC(26,2,"SIGA",.T.)
	MSCBSAY(32,33,'ENDERECO',"R","2","01,01")
	MSCBSAY(29,33,"CODIGO","R","2","01,01")
	MSCBSAY(26,33,AllTrim(SBE->BE_LOCALIZ), "R", "2", "01,01")
	MSCBSAY(22,05,"DESCRICAO","R","2","01,01")
	MSCBSAY(19,05,SBE->BE_DESCRIC,"R", "2", "01,01")
	MSCBSAYBAR(12,22,cCodigo,"R","MB07",8.36,.F.,.T.,.F.,,2,2,.F.,.F.,"1",.T.)
	MSCBInfoEti("Endereco","30X100")
MSCBEND()
Return .F.